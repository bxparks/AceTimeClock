#!/usr/bin/gawk -f
#
# Usage: generate_table.sh < ${board}.txt
#
# Takes the file generated by collect.sh and generates an ASCII
# table that can be inserted into the README.md.

BEGIN {
  labels[0] = "Baseline"
  labels[1] = "DS3231Clock<TwoWire>"
  labels[2] = "DS3231Clock<SimpleWire>"
  labels[3] = "DS3231Clock<SimpleWireFast>"
  labels[4] = "NtpClock"
  labels[5] = "EspSntpClock"
  labels[6] = "StmRtcClock"
  labels[7] = "Stm32F1Clock"
  labels[8] = "SystemClockLoop"
  labels[9] = "SystemClockLoop+1 Basic zone"
  labels[10] = "SystemClockLoop+1 Extended zone"
  labels[11] = "SystemClockCoroutine"
  labels[12] = "SystemClockCoroutine+1 Basic zone"
  labels[13] = "SystemClockCoroutine+1 Extended zone"
  record_index = 0
}
{
  u[record_index]["flash"] = $2
  u[record_index]["ram"] = $4
  record_index++
}
END {
  NUM_ENTRIES = record_index
  base_flash = u[0]["flash"]
  base_ram = u[0]["ram"]
  for (i = 0; i < NUM_ENTRIES; i++) {
    if (u[i]["flash"] == -1) {
      u[i]["d_flash"] = -1
      u[i]["d_ram"] = -1
    } else {
      u[i]["d_flash"] = u[i]["flash"] - base_flash
      u[i]["d_ram"] = u[i]["ram"]- base_ram
    }
  }

  printf(\
    "+----------------------------------------------------------------------+\n")
  printf(\
    "| Functionality                          |  flash/  ram |        delta |\n")

  for (i = 0; i < NUM_ENTRIES; i++) {
    if (u[i]["flash"] == "-1") continue

    name = labels[i]
    if (name ~ /^Baseline$/ \
        || name ~ /^DS3231Clock<TwoWire>$/ \
        || name ~ /^NtpClock$/ \
        || name ~ /^StmRtcClock$/ \
        || name ~ /^SystemClockLoop$/ \
        || name ~ /^SystemClockCoroutine$/) {
      printf(\
        "|----------------------------------------+--------------+--------------|\n")
    }
    printf("| %-38s | %6d/%5d | %6d/%5d |\n",
        name, u[i]["flash"], u[i]["ram"], u[i]["d_flash"], u[i]["d_ram"])
  }
  printf(\
    "+----------------------------------------------------------------------+\n")
}
