#!/usr/bin/gawk -f
#
# Usage: generate_table.sh < ${board}.txt
#
# Takes the file generated by collect.sh and generates an ASCII
# table that can be inserted into the README.md.

BEGIN {
  NUM_FEATURES = 7
  labels[0] = "Baseline"
  labels[1] = "DS3231Clock"
  labels[2] = "SystemClockLoop"
  labels[3] = "SystemClockLoop+1 Basic zone"
  labels[4] = "SystemClockLoop+1 Extended zone"
  labels[5] = "SystemClockCoroutine"
  labels[6] = "SystemClockCoroutine+1 Basic zone"
  labels[7] = "SystemClockCoroutine+1 Extended zone"
  record_index = 0
}
{
  u[record_index]["flash"] = $2
  u[record_index]["ram"] = $4
  record_index++
}
END {
  base_flash = u[0]["flash"]
  base_ram = u[0]["ram"]
  for (i = 0; i <= NUM_FEATURES; i++) {
    if (u[i]["flash"] == -1) {
      u[i]["d_flash"] = -1
      u[i]["d_ram"] = -1
    } else {
      u[i]["d_flash"] = u[i]["flash"] - base_flash
      u[i]["d_ram"] = u[i]["ram"]- base_ram
    }
  }

  printf(\
    "+---------------------------------------------------------------------+\n")
  printf(\
    "| Functionality                          |  flash/  ram |       delta |\n")
  printf(\
    "|----------------------------------------+--------------+-------------|\n")
  printf(\
    "| %-38s | %6d/%5d | %5d/%5d |\n",
    labels[0], u[0]["flash"], u[0]["ram"], u[0]["d_flash"], u[0]["d_ram"])
  printf(\
    "|----------------------------------------+--------------+-------------|\n")
  for (i = 1; i <= NUM_FEATURES; i++) {
    printf("| %-38s | %6d/%5d | %5d/%5d |\n",
        labels[i], u[i]["flash"], u[i]["ram"], u[i]["d_flash"], u[i]["d_ram"])
  }
  printf(\
    "+---------------------------------------------------------------------+\n")
}
